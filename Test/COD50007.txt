OBJECT Codeunit 50007 Test_DotNet_GzipStream
{
  OBJECT-PROPERTIES
  {
    Date=18-07-10;
    Time=21:29:18;
    Modified=Yes;
    Version List=;
  }
  PROPERTIES
  {
    Subtype=Test;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Assert@17024400 : Codeunit 130000;
      LibraryLowerPermissions@17024401 : Codeunit 132217;
      DotNet_GzipStream@17024405 : Codeunit 50006;

    [Test]
    PROCEDURE SampleDataCompression@17024402();
    VAR
      SourceTempBlob@17024400 : TEMPORARY Record 99008535;
      DestTempBlob@17024401 : TEMPORARY Record 99008535;
    BEGIN
      LibraryLowerPermissions.SetO365Basic;
      CLEAR(DotNet_GzipStream);
      // [Given] Uncompressed data is 'TestData'
      SourceTempBlob.WriteAsText('TestData', TEXTENCODING::UTF8);
      // [WHEN] Gzip Compression completes
      DotNet_GzipStream.InitGzipStreamFromTempBlob(DestTempBlob, TRUE);
      DotNet_GzipStream.ReadFromTempBlob(SourceTempBlob);
      DotNet_GzipStream.Close;
      // [THEN] Compressed data as Base64 should be 'H4sIAAAAAAAEAAtJLS5xSSxJBAA2+6HACAAAAA=='
      Assert.AreEqual('H4sIAAAAAAAEAAtJLS5xSSxJBAA2+6HACAAAAA==', DestTempBlob.ToBase64String, 'Compression fails');
    END;

    [Test]
    PROCEDURE SampleDataDecompression@17024400();
    VAR
      SourceTempBlob@17024400 : TEMPORARY Record 99008535;
      DestTempBlob@17024401 : TEMPORARY Record 99008535;
    BEGIN
      LibraryLowerPermissions.SetO365Basic;
      CLEAR(DotNet_GzipStream);
      // [Given] Compressed data (as Base64) is 'H4sIAAAAAAAEAAtJLS5xSSxJBAA2+6HACAAAAA=='
      SourceTempBlob.FromBase64String('H4sIAAAAAAAEAAtJLS5xSSxJBAA2+6HACAAAAA==');
      // [WHEN] Gzip Decompression completes
      DotNet_GzipStream.InitGzipStreamFromTempBlob(SourceTempBlob, FALSE);
      DotNet_GzipStream.WriteToTempBlob(DestTempBlob);
      DotNet_GzipStream.Close;
      // [THEN] Decompressed data should be 'TestData'
      Assert.AreEqual('TestData', DestTempBlob.ReadAsTextWithCRLFLineSeparator, 'Decompression fails');
    END;

    [Test]
    PROCEDURE SampleDataCompressionDecompression@17024404();
    VAR
      SourceTempBlob@17024400 : TEMPORARY Record 99008535;
      DestTempBlob@17024401 : TEMPORARY Record 99008535;
      IntermTempBlob@17024402 : TEMPORARY Record 99008535;
    BEGIN
      LibraryLowerPermissions.SetO365Basic;
      CLEAR(DotNet_GzipStream);
      // [Given] Uncompressed data is 'TestData'
      SourceTempBlob.WriteAsText('TestData', TEXTENCODING::UTF8);
      // [WHEN] Gzip Compression completes
      DotNet_GzipStream.InitGzipStreamFromTempBlob(IntermTempBlob, TRUE);
      DotNet_GzipStream.ReadFromTempBlob(SourceTempBlob);
      // [WHEN] And Gzip Decompression from the same stream completes
      CLEAR(DotNet_GzipStream);
      DotNet_GzipStream.InitGzipStreamFromTempBlob(IntermTempBlob, FALSE);
      CLEAR(DestTempBlob.Blob);
      DotNet_GzipStream.WriteToTempBlob(DestTempBlob);
      // [THEN] Result must be the same string
      Assert.AreEqual('TestData', DestTempBlob.ReadAsTextWithCRLFLineSeparator, 'Compression/Decompression fails');
    END;

    BEGIN
    END.
  }
}

